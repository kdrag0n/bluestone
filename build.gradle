buildscript {
    if (file("/usr/share/proguard/lib").exists()) {
        repositories {
            flatDir dirs: '/usr/share/proguard/lib'
        }
    }

    dependencies {
        classpath 'de.sebastianboegl.gradle.plugins:shadow-log4j-transformer:2.2.0'
        if (file("/usr/share/proguard/lib").exists())
            classpath ':proguard:'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.ajoberstar.grgit' version '3.0.0-beta.1'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'com.kdrag0n'
version '0.9.1'
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
archivesBaseName = 'bluestone'
mainClassName = 'com.kdrag0n.bluestone.Start'
applicationDefaultJvmArgs = [
        '-Xms1000M',
        '-XX:MaxDirectMemorySize=64M',
        '-XX:MaxMetaspaceSize=95800K',
        '-XX:ReservedCodeCacheSize=240M',
        '-Xss1M',
        '-Xmx3641799K',
        '-Xverify:none',
        '-Djava.lang.invoke.stringConcat=MH_INLINE_SIZED_EXACT',
        '-XX:+UseG1GC',
        '-server',
        '-XX:+CMSClassUnloadingEnabled',
        '-XX:+AggressiveOpts',
        '-XX:MaxGCPauseMillis=2500'
]

import de.sebastianboegl.gradle.plugins.shadow.transformers.Log4j2PluginsFileTransformer

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

ext {
    commit = "none"
    shortCommit = "none"
    buildTime = System.currentTimeMillis()

    if (grgit != null) {
        commit = grgit.head().id
        shortCommit = grgit.head().abbreviatedId
    }
}

buildConfig {
    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'com.kdrag0n.bluestone'
    charset = 'UTF-8'

    buildConfigField 'long', 'BUILD_UNIXTIME', buildTime + 'L'
    buildConfigField 'java.util.Date', 'BUILD_DATE', 'new java.util.Date(' + buildTime + 'L)'
    buildConfigField 'java.time.Instant', 'BUILD_INSTANT', 'java.time.Instant.ofEpochMilli(' + buildTime + 'L)'
    buildConfigField 'String', 'GIT_COMMIT', commit
    buildConfigField 'String', 'GIT_SHORT_COMMIT', shortCommit
}

shadowJar {
    transform(Log4j2PluginsFileTransformer)
    mergeServiceFiles()
    zip64 true
}

if (file("/usr/share/proguard/lib").exists()) {
    task proguard(type: proguard.gradle.ProGuardTask) {
        configuration 'proguard-rules.pro'

        injars "build/libs/bluestone-$version-all.jar"
        outjars "build/libs/bluestone-$version-pro.jar"
    }
}

dependencies {
    implementation 'net.dv8tion:JDA:3.7.1_419'
    implementation 'com.sedmelluq:lavaplayer:1.3.7'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.google.guava:guava:26.0-jre'
    implementation 'org.json:json:20180813'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'org.apache.commons:commons-lang3:3.8'
    implementation 'org.apache.commons:commons-text:1.4'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta2'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha4'
    implementation 'com.j256.ormlite:ormlite-core:5.1' // --
    implementation 'com.j256.ormlite:ormlite-jdbc:5.1'
    implementation 'com.h2database:h2:1.4.197'
    implementation 'com.sedmelluq:jda-nas:1.0.6'
    implementation 'com.zaxxer:HikariCP:3.2.0'
    implementation 'mysql:mysql-connector-java:8.0.12'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'com.jagrosh:jda-utilities-menu:2.1.4'
    implementation 'com.jagrosh:jda-utilities-commons:2.1.4'
    implementation 'com.joestelmach:natty:0.13'
    implementation 'com.twelvemonkeys.imageio:imageio-core:3.4.1'
    implementation 'com.google.re2j:re2j:1.2'
    implementation 'io.sentry:sentry-logback:1.7.7'
}